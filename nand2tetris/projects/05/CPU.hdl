// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // decode
    Or(a=instruction[15], b=false, out=isComp);
    // - compニーモックの判断(0=レジスタ, 1=メモリ)
    And(a=isComp, b=instruction[12], out=compMnemonic);
    // - ALU入力値
    And(a=isComp, b=instruction[11], out=zxIn);
    And(a=isComp, b=instruction[10], out=nxIn);
    And(a=isComp, b=instruction[9], out=zyIn);
    And(a=isComp, b=instruction[8], out=nyIn);
    And(a=isComp, b=instruction[7], out=fIn);
    And(a=isComp, b=instruction[6], out=noIn);
    // - dest
    And(a=isComp, b=instruction[5], out=loadA);
    And(a=isComp, b=instruction[4], out=loadD);
    And(a=isComp, b=instruction[3], out=writeM); // out writeM
    // - jump
    And(a=isComp, b=instruction[2], out=j1);
    And(a=isComp, b=instruction[1], out=j2);
    And(a=isComp, b=instruction[0], out=j3);

    // ARegister
    Mux16(a[0..14]=instruction[0..14], b=outMemory, sel=loadA, out=registIn);
    // - isCompが0の時は必ず登録、1の時はloadAが1の時登録
    Not(in=isComp, out=tmpIsARegist1);
    And(a=isComp, b=loadA, out=tmpIsARegist2);
    Or(a=tmpIsARegist1, b=tmpIsARegist2, out=isARegist);
    ARegister(in=registIn, load=isARegist, out=outARegister, out[0..14]=addressM, out[0..7]=outARegisterLow, out[8..15]=outARegisterHigh, out[15]=outARegisterSign); // out addressM

    // ALU
    Mux16(a=outARegister, b=inM, sel=compMnemonic, out=ALUInY);

    ALU(x=ALUInX, y=ALUInY, zx=zxIn , nx=nxIn, zy=zyIn, ny=nyIn, f=fIn, no=noIn , out=outM, out=outMemory, out[15]=outMemorySign, out[8..14]=outMemoryHigh, out[0..7]=outMemoryLow); // out outM
    Mux16(a[0..14]=instruction[0..14], b=outMemory, sel=isComp, out=inDRegister);
    DRegister(in=inDRegister, load=loadD, out=ALUInX);

    // PC
    // - out < 0 でjumpするか判断
    And(a=outMemorySign, b=true, out=isMinus);
    And(a=isMinus, b=j1, out=isJump1);
    // - out = 0でjumpするか判断
    Or8Way(in=outMemoryLow, out=outOr8Way1);
    Or8Way(in[0..6]=outMemoryHigh, out=outOr8Way2);
    Or(a=outOr8Way1, b=outOr8Way2, out=isNotZero);
    Not(in=isNotZero, out=isZero);
    And(a=isZero, b=j2, out=isJump2);
    // - out > 0でjumpするか判断
    Or(a=isMinus, b=isZero, out=isNotPlus);
    Not(in=isNotPlus, out=isPlus);
    And(a=isPlus, b=j3, out=isJump3);

    Or(a=isJump1, b=isJump2, out=isJump4);
    Or(a=isJump3, b=isJump4, out=isJump);
    Mux16(a=false, b=outARegister, sel=true, out=InPC);
    PC(in=InPC, inc=true, load=isJump, reset=reset, out[0..14]=pc); // out PC
}
